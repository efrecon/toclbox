if {"::tcltest" ni [namespace children]} {
    package require tcltest 2
    namespace import -force ::tcltest::*
}

::tcl::tm::path add [file normalize [file join [file dirname [info script]] ..]]
package require toclbox::options

test options-1.1 {Parsing bool option} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -oneopt
} -result "1"

test options-2.1 {Parsing an option} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -another -value myvar
    set myvar
} -result "value"

test options-2.2 {Finding an option} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -another
} -result 1

test options-3.1 {Parsing an inexisting option} -match glob -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -inexisting -value anothervar
    set anothervar
} -returnCodes 1 -result {can't read*}

test options-4.1 {Not finding an inexisting option} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -inexisting
} -result 0

test options-5.1 {Parsing an inexisting option, getting default} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -inexisting -default "something" -value myvar
    set myvar
} -result "something"

test options-6.1 {Parsing an option, old style} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -another myvar
    set myvar
} -result "value"

test options-7.1 {Parsing an inexisting option, getting default, old style} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -inexisting myvar "something"
    set myvar
} -result "something"

test options-8.1 {Finding an incomplete option} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -anot
} -result 1

test options-9.1 {Getting the first incomplete option} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -a -option which
    set which
} -result -another

test options-10.1 {Finding an option, but omitting the leading dash} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts another -option parsed
    set parsed
} -result -another

test options-11.1 {Checking removal of option from list} -body {
    set opts [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::parse opts -another
    lsearch $opts -another
} -result -1


test options-20.1 {Checking pulling of options} -body {
    set allargs [list -oneopt -another value -athird itsvalue -- anarg anotherarg]
    ::toclbox::options::pull allargs opts
    return [list $allargs $opts]
} -result {{anarg anotherarg} {-oneopt -another value -athird itsvalue}}

test options-21.1 {Checking pulling of options, with no arguments} -body {
    set allargs [list -oneopt -another value -athird itsvalue]
    ::toclbox::options::pull allargs opts
    return [list $allargs $opts]
} -result {{} {-oneopt -another value -athird itsvalue}}

test options-22.1 {Checking pulling of options, with no options} -body {
    set allargs [list -- anarg anotherarg]
    ::toclbox::options::pull allargs opts
    return [list $allargs $opts]
} -result {{anarg anotherarg} {}}

test options-23.1 {Checking pulling of options, with nothing after double dash} -body {
    set allargs [list -oneopt -another value -athird itsvalue --]
    ::toclbox::options::pull allargs opts
    return [list $allargs $opts]
} -result {{} {-oneopt -another value -athird itsvalue}}

test options-24.1 {Checking pulling of options, with no options and without double dash} -body {
    set allargs [list anarg anotherarg]
    ::toclbox::options::pull allargs opts
    return [list $allargs $opts]
} -result {{anarg anotherarg} {}}

test options-30.1 {Checking integer} -body {
    set A(-test) 1
    ::toclbox::options::check A -test integer
} -result {}

test options-31.1 {Checking non-integer} -body {
    set A(-test) 1.4
    ::toclbox::options::check A -test integer
} -result {-test integer}

test options-32.1 {Checking non-integer (empty)} -body {
    set A(-test) ""
    ::toclbox::options::check A -test integer
} -result {-test integer}

test options-33.1 {Checking missing dash} -body {
    set A(-test) ""
    ::toclbox::options::check A test integer
} -result {-test integer}

test options-40.1 {Checking push end and start} -body {
    set allargs [list -oneopt -another value -athird< bingo -athird itsvalue -athird> toto]
    catch {unset A}
    array set A {}
    ::toclbox::options::push allargs -athird A
    array get A
} -result {-athird {bingo itsvalue toto}}

test options-41.1 {Checking reset with push end and start} -body {
    set allargs [list -oneopt -another value -athird< bingo -athird itsvalue -athird therealvalue -athird> toto]
    catch {unset A}
    array set A {}
    ::toclbox::options::push allargs -athird A
    array get A
} -result {-athird {bingo therealvalue toto}}

test options-41.1 {Checking no value} -body {
    set allargs [list -oneopt -another value -athird< bingo -athird itsvalue -athird therealvalue -athird> toto]
    catch {unset A}
    array set A {}
    ::toclbox::options::push allargs -nonexisting A
    array get A
} -result {}

cleanupTests